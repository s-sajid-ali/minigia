//
// MADX Example 2: FODO cell with dipoles
// Author: V. Ziemann, Uppsala University
// Date: 060911

TITLE,'Example 2: FODO2.MADX'; 

BEAM, PARTICLE=ELECTRON,PC=3.0;     

DEGREE:=PI/180.0;                  // for readability

QF: QUADRUPOLE,L=0.5,K1=0.2;       // still half-length
QD: QUADRUPOLE,L=1.0,K1=-0.2;      // changed to full length
B: SBEND,L=1.0,ANGLE=15.0*DEGREE;  // added dipole

FODO: SEQUENCE,REFER=ENTRY,L=12.0;
  QF1:   QF,      AT=0.0;
  B1:    B,       AT=2.5;
  QD1:   QD,      AT=5.5;
  B2:    B,       AT=8.5;
  QF2:   QF,      AT=11.5;
ENDSEQUENCE;

USE, PERIOD=FODO;


//MATCH, SEQUENCE=FODO;           
SELECT,FLAG=SECTORMAP,clear;
SELECT,FLAG=TWISS,column=name,s,betx,bety;
TWISS, file=optics.dat,sectormap;

PLOT,HAXIS=S, VAXIS=BETX, BETY, DX, INTERPOLATE=TRUE;
//PLOT,HAXIS=S, VAXIS=DX, INTERPOLATE=TRUE;

Value, TABLE(SUMM,Q1);
Value, TABLE(SUMM,Q2);

